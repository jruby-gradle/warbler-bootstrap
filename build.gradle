plugins {
    id "com.jfrog.bintray" version "1.0"
}

apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'java'

version = '0.2.0'
group = 'com.github.jruby-gradle'
description = 'war bootstrap code for launching JRuby web apps'
sourceCompatibility = '1.6'
targetCompatibility = '1.6'

if (System.env.RELEASE != '1') {
    version = "${version}-SNAPSHOT"
}

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'cglib:cglib-nodep:2.2.+'
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

bintray {
    user = project.bintrayUser
    key = project.bintrayKey
    publish = true
    dryRun = false
    configurations = ['archives']

    pkg {
        repo = 'libraries'
        userOrg = 'jruby-gradle'
        name = 'warbler-bootstrap'
    }
}
bintrayUpload.dependsOn assemble

// vim: ft=groovy
